WHITESPACE = _{ " " | "\t" | NEWLINE }

program = { SOI ~ begin ~ statement* ~ end ~ EOI }
statement   = _{ 
  var_decl 
  | write
  | assignment 
  | procedure
  | procedure_call }

/// BEGIN and END
begin = { "BEGIN" ~ semicolon }
end = { "END" ~ semicolon }

/// WRITE
write = { "WRITE" ~ expr+ ~ semicolon }

/// VARIABLE
var_decl = { "VARIABLE" ~ variable_name ~ variable_length ~ semicolon }
variable_name = @{ !keyword ~ ASCII_ALPHANUMERIC+ }
variable_length = @{ ASCII_DIGIT+ }

/// Keywords - define all reserved words
keyword = { "BEGIN" | "END" | "WRITE" | "VARIABLE" | "PROCEDURE" | "ENDPROCEDURE" | "EXP" }

/// [ PROCEDURES ]
  /// Container
  procedure = { start_procedure ~ statement+ ~ end_procedure }
  /// Calling - ensure the name is not a keyword
  procedure_call = { procedure_name ~ expr* ~ semicolon }
  /// PROCEDURE
  start_procedure = { "PROCEDURE" ~ procedure_name ~ procedure_argument_name* ~ semicolon }
  procedure_name = @{ !keyword ~ ASCII_ALPHANUMERIC+ }
  procedure_argument_name = @{ !keyword ~ ASCII_ALPHANUMERIC+ }
  /// ENDPROCEDURE
  end_procedure = { "ENDPROCEDURE" ~ semicolon }

/// [ FUNCTIONS ]
  /// EXP
  exp = { "EXP(" ~ expr ~ ")" }

/// Assignment
assignment  = { identifier ~ ":=" ~ expr ~ semicolon }

// Expression parsing using Pratt parser
expr = { term ~ (infix_op ~ term)* }
term = _{ exp | 
    number | 
    identifier |
    "(" ~ expr ~ ")" }

// Infix operators
infix_op    = _{ add }
add         = { "+" }

identifier  = @{ !keyword ~ ASCII_ALPHANUMERIC+ }
number      = @{ "-"? ~ ASCII_DIGIT+ }
semicolon   = { ";" }